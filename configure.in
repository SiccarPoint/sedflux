AC_PREREQ([2.59])

AC_INIT([ew],[2.0],[huttone@colorado.edu])
AC_CONFIG_HEADERS(config.h:config-h.in)
AC_CONFIG_AUX_DIR([config])
AC_CONFIG_MACRO_DIR([m4])

AM_INIT_AUTOMAKE([1.9.6 foreign])

m4_define([glib_required_version],[2.12.0])
m4_define([gtk_required_version],[2.2.0])
m4_define([check_required_version],[0.9.4])

PKG_PROG_PKG_CONFIG([0.20])

PKG_CHECK_MODULES(GLIB,[glib-2.0 >= glib_required_version])

DOXYGEN_VERSION="(null)"
CHECK_VERSION="(null)"
GTK_VERSION="(null)"

AC_ARG_ENABLE(libcheck,
   [AC_HELP_STRING([--enable-libcheck], [compile unit tests using check])], ,
   [enable_libcheck=no])

if test x$enable_libcheck != xno ; then
   PKG_CHECK_MODULES(CHECK,[check >= check_required_version],
      CHECK_VERSION=`$PKG_CONFIG --modversion check`,
      AC_MSG_ERROR([
*** Checks for check failed.  You can build without it by passing
*** --without-libcheck to configure but some programs using GTK+ will
*** not work properly.]) )
fi

AM_CONDITIONAL(ENABLE_CHECK, test x$CHECK != x )


AC_ARG_ENABLE(libgtk,
   AC_HELP_STRING([--enable-libgtk], [compile GUI using GTK+-2.0]), ,
   [enable_libgtk=no])

if test x$enable_libgtk != xno ; then
   PKG_CHECK_MODULES(GTK,[gtk+-2.0 >= gtk_required_version],
      GTK_VERSION=`$PKG_CONFIG --modversion gtk+-2.0`,
      AC_MSG_ERROR([
*** Checks for GTK+-2.0 failed.  You can build without it by passing
*** --without-libgtk to configure but some programs using GTK+ will
*** not work properly.]) )
fi

AM_CONDITIONAL(ENABLE_GTK, test x$GTK != x )



# specify the path to install HTML docs
#   HTML_DIR
# should we build documentation
#   ENABLE_DOXYGEN

AC_ARG_WITH(html-dir,
   AC_HELP_STRING([--with-html-dir=PATH], [path to installed docs]),,
   [with_html_dir='${datadir}/ew-doc/html'])

HTML_DIR="$with_html_dir"
AC_SUBST(HTML_DIR)

AC_ARG_ENABLE(doxygen,
   AC_HELP_STRING([--enable-doxygen],
                  [use doxygen to build documentation [default=no]]),,
   [enable_doxygen=no])

if test -x$enable_doxygen != xno ; then

   AC_CHECK_PROGS(DOXYGEN, doxygen )

   if test x$DOXYGEN != x ; then
      DOXYGEN_VERSION=`$DOXYGEN --version`
   else
      AC_MSG_ERROR([
*** Program Doxygen was not found.  If you don't need to generate documentaion,
*** use the --disable-doxygen option when configuring.  Otherwise, if you
*** have doxygen, make sure it is in your path. ])
   fi

fi

AM_CONDITIONAL(ENABLE_DOXYGEN, test x$enable_doxygen = xyes )

AC_PROG_CC
AC_PROG_CC_STDC
AC_PROG_LIBTOOL

AC_HEADER_STDC

AC_C_CONST
AC_TYPE_PID_T
AC_TYPE_SIZE_T

cflags_set="$CFLAGS"

GLIB_ADD_TO_VAR(CFLAGS, ${GLIB_CFLAGS} , ${GLIB_CFLAGS} )
GLIB_ADD_TO_VAR(LIBS, -lm , -lm )

AM_PATH_GLIB_2_0( glib_required_version , :,
   AC_MSG_ERROR([
*** GLIB glib_required_version or better is required. The latest version of
*** GLIB is always available from ftp://ftp.gtk.org/pub/gtk/.]),
  gobject gmodule-no-export gthread)

AC_SUBST( GLIB_LIBS )
AC_SUBST( GLIB_CFLAGS )
AC_SUBST( GTK_LIBS )
AC_SUBST( GTK_CFLAGS )
AC_SUBST( CHECK_LIBS )
AC_SUBST( CHECK_CFLAGS )

AC_CONFIG_FILES([ libutils/src/utils.pc ])
AC_CONFIG_FILES([ libsed/src/sedflux.pc ])

AC_CONFIG_FILES([Makefile \
           libutils/Makefile libutils/src/Makefile \
           libsed/Makefile libsed/src/Makefile libsed/share/Makefile \
           avulsion/Makefile avulsion/src/Makefile \
           bing/Makefile bing/src/Makefile \
           compact/Makefile compact/src/Makefile compact/data/Makefile \
           diffusion/Makefile diffusion/src/Makefile diffusion/data/Makefile \
           failure/Makefile failure/src/Makefile \
           flow/Makefile flow/src/Makefile flow/mfiles/Makefile \
           inflow/Makefile inflow/src/Makefile inflow/data/Makefile inflow/mfiles/Makefile \
           muds/Makefile muds/src/Makefile muds/data/Makefile \
           plume/Makefile plume/src/Makefile plume/demo/Makefile plume/mfiles/Makefile \
           quake/Makefile quake/src/Makefile \
           sakura/Makefile sakura/src/Makefile sakura/mfiles/Makefile sakura/data/Makefile \
           squall/Makefile squall/src/Makefile squall/data/Makefile \
           subside/Makefile subside/src/Makefile \
           xshore/Makefile xshore/src/Makefile \
           sedflux/Makefile sedflux/src/Makefile sedflux/data/Makefile sedflux/mfiles/Makefile \
           hydrotrend/Makefile hydrotrend/src/Makefile \
              hydrotrend/data/Makefile hydrotrend/mfiles/Makefile \
           sedutils/Makefile sedutils/src/Makefile \
           inflow/src/runinflow \
           sakura/src/runsakura \
           sedflux/src/runsedflux \
           sedflux/src/runsedflux-batch \
           docs/sedflux-doxyfile \
           docs/Makefile])

AC_OUTPUT

echo \
"------------------------------------------------------------------------
Configuration:

  Source code location:       ${srcdir}
  Compiler:                   ${CC}
  Compiler flags:             ${cflags_set}
  Host System Type:           ${host}
  Install path:               ${prefix}
  With gtk+-2.0:              ${GTK_VERSION}
  With check:                 ${CHECK_VERSION}
  With doxygen:               ${DOXYGEN_VERSION}
  HTML path:                  ${HTML_DIR}

  See config.h for further configuration information.
------------------------------------------------------------------------"

